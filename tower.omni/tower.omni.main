#script.name Omni-Tower AI
:name {script.name}

:import tower.omni.templates

:budget_cap max

wakeup()
game.newround()

gotoif(if(health(false) <= 0., 99, start), impulse() != "game.newround" && impulse() != "wakeup")
execute("{accelerate_budget}")

start:
  FPS.target = 60.
  inf.secures = {inf.secures}
  pause() ; Set the game on pause for safety

software.init: ; Disable all software (except New Bounds, No Bounds, Wave Restart)
	{software.toggle.single(3, software.index)}
  software.index = (software.index + 1) % {software.count}
  gotoif(software.init, software.index > 0)

lag.saver: ; Pause the game for 1 second in the beginning and during lags
  pause.set(1. / time.unscaled() < FPS.target || game.realtime() < 1.)

era.disables:
  gotoif(era.dividers.init, finished.era.disables)
  {disable.era.next({era.elements})}
	gotoif(era.disables, {disable.exists.free({era.elements})}) ; Disable all free era powers first
  finished.era.disables = {disable.cost.total({era.elements})} <= -10.

era.dividers.init:
  gotoif(infinity, {finished.era.dividers} || xp() < 40000.)
  xp = xp()
  upgrade.era("damage", 1)
  era.damage = if(era.damage == 0., if(xp <= xp(), -1., xp - xp()), era.damage)
  upgrade.era("health", 1)
  era.health = if(era.health == 0., xp - max(0., era.damage) - xp(),\
               if(xp >= {era.divider.cost.max} && xp() >= xp, -1., era.health))

infinity:
  gotoif(borf, xp() < disable.inf.cost() || {finished.inf})
  disable.inf(sub(inf.secures, 0, index(inf.secures, ",", 0)))
  inf.secures = sub(inf.secures, index(inf.secures, ",", 0) + 1, 999)

borf:
  useinstant(active.index("spell.superbounce"))
  useinstant(active.index("spell.multishot"))
  
energy = energy(false) ; Save current energy for future use

rak.start: ; Skip this section if the BP has no Rak's Curse or we have 5 stacks already
  goto(if({finished.rak}, spells, if(energy(true) < 0.99, frame.end, rng.init)))

rng.init:
  {rng_init1}
  {rng_init2(rng.next)}

rng.next:
  {rng_next}
  gotoif(rnd(rng.next, 0), {rng_getrange(0, 999)} > 0)

rak.cast:
  useinstant(active.index("spell.raksCurse"))
  counter.rak += if(energy > energy(false), 1, 0) ; If energy didn't change, we didn't cast Rak's curse
  goto(frame.end)

; Skill spam, ignores toggleable skills and Rak's Curse;
; Has special handling of Dark Sacrifice, Advanced Heal and Dispel
spells:
  spell.cast.index = if({spell.check} && spell.index > 0, spell.cast.index, spell.index)
  spell.index = if(energy / energy.max() < 0.7, 0, (spell.index + 1) % (active.count() + 1))
  gotoif(spells, spell.index > 0)

cast:
  useinstant(if({heal.check}, active.index("spell.advancedheal"), 0))
  useinstant(spell.cast.index)
  useposition(spell.cast.index, vec(0., 0.))

; All skills need to be active and era dividers need to be maximized
gotoif(frame.end, {finished.software} || {spell.pending.check} || era.damage + era.health >= 0.)
software.main: ; Enable all software (except New Bounds, No Bounds, Wave Restart)
	{software.toggle.single(0, software.index)}
  software.index += 1
  gotoif(software.main, software.index < {software.count})

frame.end:
  waitframe()
  gotoif(lag.saver, isTowerTesting())
